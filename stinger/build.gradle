plugins {
    id "application"
}

def SUPPORTED_ARCHES = ['windowsx86-64', 'linuxx86-64']

sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()

    maven {
        url('https://frcmaven.wpi.edu/artifactory/release')
    }
}

configurations {
    nativeZip
    implementation.extendsFrom nativeZip
}

dependencies {
    implementation project(':framework')

    implementation 'com.github.tomtzook:castle:1.0.0'
    implementation "com.google.code.gson:gson:2.8.5"
    implementation group: 'com.1stleg', name: 'jnativehook', version: '2.1.0'
    runtimeOnly group: 'org.xerial', name: 'sqlite-jdbc', version: '3.14.2'

    implementation group: "edu.wpi.first.thirdparty.frc${YEAR}.opencv", name: 'opencv-java', version: "$OPENCV_VERSION"

    SUPPORTED_ARCHES.forEach({
        nativeZip group: "edu.wpi.first.thirdparty.frc${YEAR}.opencv", name: 'opencv-jni', version: "$OPENCV_VERSION", classifier: "$it", transitive: false
    })
}

application {
    mainClassName = 'stinger.Main'
}

task copyNatives(type: Copy) {
    from {
        configurations.nativeZip.collect {
            zipTree(it).matching {
                include '**/*.so'
                include '**/*.dll'
            }.files
        }
    }
    into project.file("natives")
}

project.processResources.dependsOn copyNatives
